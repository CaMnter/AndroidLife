apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'me.tatarka.retrolambda'
// apply from: rootProject.getRootDir().getAbsolutePath() + "/common.gradle"
// apply plugin: RocooFixPlugin
apply plugin: 'com.camnter.gradle.plugin.life'

buildscript {
    repositories {
        jcenter()
        mavenLocal()
        maven { url uri('../gradle-plugin-life/repository') }
    }
    dependencies {
        classpath 'com.camnter.gradle.plugin.life:gradle-plugin-life:1.0.0'
    }
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

repositories {
    //    maven { url 'http://uk.maven.org/maven2' }
    //    maven { url 'https://raw.githubusercontent.com/umeng/mvn-repo-umeng/master/repository' }
    jcenter()
    mavenCentral()
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        applicationId "com.camnter.newlife"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        // debug的签名
        debug {
            storeFile file("../camnter.jks")
            storePassword "123456"
            keyAlias "camnter"
            keyPassword "123456"
        }
    }
    buildTypes {
        release {
            println("====== buildTypes.release ======")
            //            minifyEnabled true
            //            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            manifestPlaceholders = [DEBUG: "release"]
        }
        debug {
            println("====== buildTypes.debug ======")
            signingConfig signingConfigs.debug
            manifestPlaceholders = [DEBUG: "debug"]

            // app-debug-androidTest-unaligned.apk
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "app-debug-androidTest-unaligned.apk"
                        println("====== buildTypes.output ======")
                        println("AppName: " + fileName)
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    //    // 渠道Flavors
    //    productFlavors {
    //        github {
    //            applicationId "com.camnter.newlife"
    //            manifestPlaceholders = [DEBUG: "debug"]
    //        }
    //    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        resultsDir = "${project.buildDir}/life/results"
    }

    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets', 'src/main/assets/']
            jniLibs.srcDirs = ['libs']
        }
    }

    dataBinding {
        enabled = true
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi-v7a'
            universalApk true
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    testCompile rootProject.ext.denpendencies["junit"]
    // 如果要使用 Mockito，你需要添加此条依赖库
    testCompile rootProject.ext.denpendencies["mockito-core"]
    // 如果你要使用 Mockito 用于 Android instrumentation tests，那么需要你添加以下三条依赖库
    androidTestCompile rootProject.ext.denpendencies["mockito-core"]
    androidTestCompile rootProject.ext.denpendencies["dexmaker"]
    androidTestCompile rootProject.ext.denpendencies["dexmaker-mockito"]
    androidTestCompile(rootProject.ext.denpendencies["espresso-core"], {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-v7'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.google.code.findbugs'
    })

    // Google
    compile rootProject.ext.denpendencies["support-v4"]
    compile rootProject.ext.denpendencies["support-v7"]
    compile rootProject.ext.denpendencies["recyclerview-v7"]
    compile rootProject.ext.denpendencies["design"]
    compile rootProject.ext.denpendencies["gson"]
    compile rootProject.ext.denpendencies["guava"]
    // camnter
    compile rootProject.ext.denpendencies["easyslidingtabs"]
    compile rootProject.ext.denpendencies["easyrecyclerview"]
    compile rootProject.ext.denpendencies["easyarcloading"]
    compile rootProject.ext.denpendencies["easylikearea"]
    compile rootProject.ext.denpendencies["easyrecyclerviewsidebar"]
    // ReactiveX
    compile rootProject.ext.denpendencies["rxandroid2"]
    compile rootProject.ext.denpendencies["rxjava2"]
    // Square
    compile rootProject.ext.denpendencies["retrofit"]
    compile rootProject.ext.denpendencies["picasso"]
    // Alibaba
    compile rootProject.ext.denpendencies["fastjson"]
    // Glide
    compile rootProject.ext.denpendencies["glide"]

    // databinding
    annotationProcessor rootProject.ext.annotationProcessor["databinding-compiler"]
    // butterknife
    compile rootProject.ext.denpendencies["butterknife"]
    annotationProcessor rootProject.ext.annotationProcessor["butterknife-compiler"]

    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'

    compile project(':otto')
    compile project(':volley')
    compile project(':agera-1.0.0')
    compile project(':stackblur')
    compile project(':hotfix')
    compile project(':rxjava2')
    compile project(':databinding')
    compile project(':databinding-rxjava')
    compile project(':widget')
    compile project(':utils')
    compile project(':smartsave')
    compile project(':annotation-processor-annotation')
    compile project(':smartrouter')

    testCompile project(':utils')

    // annotation-processor-compiler
    annotationProcessor project(':annotation-processor-compiler')
    // smartsave-compiler
    annotationProcessor project(':smartsave-compiler')
    // smartrouter-compiler
    annotationProcessor project(':smartrouter-compiler')
}

task nativeLibsToJar(type: Zip, description: 'create a jar archive of the native libs') {
    destinationDir file("$buildDir/native-libs")
    baseName 'native-libs'
    extension 'jar'
    from fileTree(dir: 'libs', include: '**/*.so')
    from fileTree(dir: 'renderscript', include: '**/*.so')
    into 'lib/'
}

tasks.withType(JavaCompile) { compileTask -> compileTask.dependsOn(nativeLibsToJar)
}
